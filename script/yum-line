#!/usr/bin/env perl

=head1 NAME

yum-line - Utility for managing staged-release yum repositories

=cut

use strict;
use warnings;
use v5.10.0;

use Term::ReadKey;
use Yum::Line;
use Yum::Line::ResultSet;

package Yum::Line::Script;

use JSON::XS qw(decode_json);

use Moo;
use MooX::Cmd;
use MooX::Options protect_argv => 0;

extends 'Yum::Line';

option assumeno => (
	is    => 'ro',
	short => 'n',
);

option assumeyes => (
	is    => 'ro',
	short => 'y',
);

option configfile => (
	is      => 'ro',
	format  => 's',
	short   => 'c',
	default => './etc/config.json',
);

option disabletrain => (
	is        => 'ro',
	format    => 's@',
	autosplit => ',',
	short     => 't',
	default   => sub { [] },
);

sub _build_config {
	my $self = shift;
	return $self->_read_json_file($self->configfile);
}

around [qw(options_help options_man)] => sub {
	my ( $orig, $self, $code, @message ) = @_;
	system 'perldoc', __FILE__;
	exit($code // 0);
};

around [qw(options_short_usage options_usage)] => sub {
	my ( $orig, $self, $code, $usage ) = @_;
	say "USAGE: yum-line [-chnty] <command> [args1] [arg2] ...";
	say "COMMANDS AVAILABLE: clean, cleanable, dump, init, list, listall, load, loadable,";
	say	"    promotable, promote, sync, sync-obsolete";
	exit($code // 0);
};

=head1 SYNOPSIS

    # Execute command 'command' with options and additional, command specific
    # arguments
    yum-line [options] <command> [arg1] [arg2] ...
    
    # Initialise yum repositories
    yum-line init
    
    # Synchronise upstream repositories to local mirrors
    yum-line sync
    
    # Mirror obsolete upstream OS repositories
    yum-line sync-obsolete
    
    # Load new packages from upstream mirrors into 'stop' repositories
    yum-line load <stop> [package1] [package2] ...
    
    # Promote packages from 'stop' to next stage repositories
    yum-line promote <stop> [package1] [package2] ...
    
    # Clean older packages from 'stop' repositories
    yum-line clean <stop> [package1] [package2] ...
    
    # List packages available from repository
    yum-line list <repo> [package1] [package2] ...
    
    # Dump understood configuration
    yum-line dump

=head1 OPTIONS

=over 4

=item -c --configfile

File containing repo line configuration. See L<Yum::Line> for more information.

=item -t --disabletrain=<train1>,<train2>,...

Ignore trains matching the listed trains or train globs.

=item -h --help

Show help text.

=item -n --assumeno

Assume no; assume the answer to any question asked is "no".

=item -y --assumeyes

Assume yes; assume the answer to any question asked is "yes".

=back

=head1 COMMANDS

=head2 init

Initialise the line repositories. Creates an empty repository for each line-stop
pair if it does not exist. Links each os-stop repository to the upstream
os-version repository. These links will be dangling if L</sync>/L</sync-obsolete>
have not been run.

=head2 sync

Synchronise local mirrors of upstream repositories. For each upstream repository,
retrieve the current set of packages so the mirror matches upstream exactly. If
an upstream definition does not have a sync location, the repository is left
alone. Upstreams that are marked obsolete are also left alone.

=head2 sync-obsolete

Synchronise local mirrors of obsolete OS repositories. Does the same as L</sync>,
but for upstreams that are marked obsolete. This command is split from sync under
the assumption that obsolete repositories do not change so it is a waste to sync
them.

=head2 load, loadable

If run without any packages, load will load every available updated package into
the specified stop repositories. If one or more packages or package globs are
specified, yum-line will only load the listed packages. While loading packages,
yum-line will B<not> ensure that any dependencies are satisfied.

To show what load would do, the loadable command displays the changes, but does
not actually apply them.

=head2 promote, promotable

If run without any packages, promote will move the latest version of each package
update available in the specified stop to the next stop.  If one or more packages
or package globs are specified, yum-line will only promote the listed packages.
While promoting packages, yum-line will B<not> ensure that any dependencies are
satisfied.

To show what promote would do, the promotable command displays the changes, but does
not actually apply them.

=head2 clean, cleanable

If run without any packages, clean will remove packages from the specified stop
where there is a newer version available at that point in the line. If one or
more packages or package globs are specified, yum-line will only clean the listed
packages.

To show what clean would do, the cleanable command displays the changes, but does
not actually apply them.

To understand better how clean works, consider a line with three stops: dev,
test, and prod. The package foo has multiple versions:
  dev:  foo-1.0, foo-1.6
  test: foo-1.2, foo-1.3
  prod: foo-1.1, foo-1.4

'clean dev' will remove foo-1.0 because foo-1.6 is available.

'clean test' will remove foo-1.2 and foo-1.3 because foo-1.4 is available.

'clean prod' will remove foo-1.1 because foo-1.4 is available.

=head2 list, listall

Show the packages available in the specified repository.

The listall command will show all versions for each package, rather than just
the latest.

=head2 dump

The dump command prints the line configuration as understood by yum-line. It is
useful for confirming that the configuration file is correct.

=cut

package main;

my $line = Yum::Line::Script->new_with_options;

my $command = shift;

if (!$command) {
	die "Must pass command\n";

} elsif ($command eq 'clean' || $command eq 'cleanable') {
	my $stop = shift;

	stop_action($stop, 'cleanable', $command, @ARGV);

} elsif ($command eq 'init') {
	my $stop = shift;

	foreach my $train ($line->base, map $line->train($_), $line->train_names) {
		say 'Initializing ', $train->name;
		print $train->init;
	}

} elsif ($command eq 'list' || $command eq 'listall') {
	my $name = shift;
	my $repo = $line->repo($name);

	my $results = filtered_results(@ARGV);

	say_package_header();
	foreach my $name ($repo->package_names) {
		my @all = $repo->package_all($name);
		my $package = shift @all;
		next if (!$results->want_package($package));
		@all = () unless ($command eq 'listall');
		say_package($package);
		say_package($_, 1) foreach (@all);
	}

} elsif ($command eq 'load' || $command eq 'loadable') {
	my $stop = shift;

	stop_action($stop, 'loadable', $command, @ARGV);

} elsif ($command eq 'promote' || $command eq 'promotable') {
	my $stop = shift;

	stop_action($stop, 'promotable', $command, @ARGV);

} elsif ($command eq 'sync') {
	$line->sync(0);

} elsif ($command eq 'sync-obsolete') {
	$line->sync(1);

} elsif ($command eq 'dump') {
	say 'Config file: ', $line->configfile;
	say 'Base';
	say_base($line->base);

	say "\nUpstream Repositories";
	foreach my $name ($line->upstream_names) {
		say_repo($line->upstream($name));
	}

	say "\nTrain Repositories";
	foreach my $name ($line->train_names) {
		say_train($line->train($name));
	}
} else {
	die "Unknown command \"$command\"\n";
}
exit 0;


sub filtered_results {
	my (@filter_specs) = @_;

	my $re = sub {
		my $spec = shift;
		die "Invalid filter: $spec\n" unless ($spec =~ /^[-?*\w]+$/);
		$spec =~ s/\?/./g;
		$spec =~ s/\*/.*/g;
		return qr/^$spec$/;
	};

	my @filters;
	foreach my $spec (@filter_specs) {
		$spec = &$re($spec);
		push @filters, sub { $_[0]->name =~ $spec; };
	}
	my @disable_trains = map &$re($_), @{ $line->disabletrain };
	my $results = Yum::Line::ResultSet->new(
		filters => \@filters,
		train_filters => [sub {
			my $train = shift;
			return !grep $train =~ $_, @disable_trains;
		}],
	);
}

sub say_base {
	my $base = shift;

	say '   Name ', $base->name;
	say '   Base ', $base->base;
	say '   Source ', $base->source;
	say '   Version ', $base->version;
	say '   Uses ', join (', ', @{ $base->_upstream_names });
	say '   Train';
	say "      $_->{name} $_->{version}" foreach ($base->stop_definitions);
}

sub say_package {
	my ($package, $secondary) = @_;

	foreach my $file ($package->packages) {
		say sprintf '  %-50s %-15s %-15s %-12s %-10s %s',
			($secondary ? '' : $file->name),
			$file->version,
			$file->release,
			$file->arch,
			$file->repo // '',
			$file->file;
	}
}

sub say_package_header {
	say sprintf '  %-50s %-15s %-15s %-12s %-10s', qw(Name Version Release Arch Repo);
}

sub say_repo {
	my $repo = shift;

	say '   Name ', $repo->name;
	say $repo->source
		? '      Source '. $repo->source_subbed
		: '      Local';
	say '      Directory ', $repo->directory;
	say '      Arch ', $repo->arch;
	say '      Rel ', $repo->rel;
	say '      Only Packages ', join (', ', $repo->restrict)
		if ($repo->restrict);
}

sub say_result {
	my $result = shift;

	foreach my $train ($result->trains) {
		say 'For train ', $train;
		say_package_header();
		foreach my $package ($result->packages($train)) {
			say_package($package);
		}
	}
}

sub say_train {
	my $train = shift;

	say '   Name ', $train->name;
	say '      Base ', $train->base;
	say '      Uses ', join (', ', $train->upstream_names);
}

sub stop_action {
	my ($stop, $check, $command, @filters) = @_;

	my $results = filtered_results(@filters);

	my $candidates = $line->$check($stop, $results);
	say_result($candidates);
	exit if ($check eq $command);

	my $answer = y_n('Is this OK?');
	if ($answer eq 'y') {
		print $line->$command($stop, $candidates);
	}
}

sub y_n {
	my $question = shift;

	return 'n' if ($line->assumeno);
	return 'y' if ($line->assumeyes);

	my $answer;
	while (1) {
		print "$question [y/N] ";
		$answer = lc(ReadLine(0));
		chomp $answer;
		$answer = 'n' if (!$answer);
		last if ($answer eq 'n' || $answer eq 'y');
	}

	return $answer;
}
